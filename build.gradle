plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.22'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.fazecast:jSerialComm:2.10.4'

    testImplementation platform('org.junit:junit-bom:5.10.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

java {
    withJavadocJar()
    withSourcesJar()
}

kotlin {
    jvmToolchain(17)
}

test {
    systemProperty "test.setup", "Allschwil"
    systemProperty "java.library.path", file("src/test/java/org/llschall/ardwloop/jni").absolutePath
    useJUnitPlatform()
}

boolean publish = project.hasProperty('token_usr')

if (!publish) {
    println('=== Publish skipped because no credential is provided. ===')
}

publishing {
    if (publish) {
        publications {
            ardwloop(MavenPublication) {
                from components.java

                groupId = 'io.github.llschall'
                artifactId = 'ardwloop'
                version = '0.1.4'

                pom {
                    signing {
                        sign publishing.publications.ardwloop
                        sign configurations.archives
                    }
                    name = 'ardwloop'
                    description = 'ardwloop takes care of exchanging a matrix of data between your Java (or Kotlin) program and its matching Arduino program.'
                    url = 'https://github.com/llschall/ardwloop'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'llschall'
                            name = 'Laurent Schall'
                            email = 'llschall@gmx.com'
                        }
                    }
                    scm {
                        url = 'https://github.com/llschall/ardwloop.git'
                    }
                }
            }
        }
        repositories {
            maven {
                credentials {
                    username "$token_usr"
                    password "$token_pwd"
                }
                url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            }
        }
        signing {
            sign publishing.publications.ardwloop
        }
    }
}