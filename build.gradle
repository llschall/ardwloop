plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.0.0'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.fazecast:jSerialComm:2.10.4'

    testImplementation platform('org.junit:junit-bom:5.10.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    withJavadocJar()
    withSourcesJar()
}

kotlin {
    jvmToolchain(21)
}

test {
    systemProperty "test.setup", "Allschwil"
    systemProperty "java.library.path", file("src/test/java/org/llschall/ardwloop/jni").absolutePath
    useJUnitPlatform()
}

boolean publish = project.hasProperty('token_usr')

if (!publish) {
    println('=== Publish skipped because no credential is provided. ===')
}

publishing {
    if (publish) {
        publications {
            ardwloop(MavenPublication) {
                from components.java

                groupId = 'io.github.llschall'
                artifactId = 'ardwloop'
                version = '0.2.3-SNAPSHOT'

                pom {
                    signing {
                        sign publishing.publications.ardwloop
                        sign configurations.archives
                    }
                    name = 'ardwloop'
                    description = 'ardwloop takes care of exchanging a matrix of data between your Java (or Kotlin) program and its matching Arduino program.'
                    url = 'https://llschall.github.io/ardwloop'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'llschall'
                            name = 'Laurent Schall'
                            email = 'llschall@gmx.com'
                        }
                    }
                    scm {
                        url = 'https://github.com/llschall/ardwloop.git'
                    }
                }
            }
        }
        repositories {
            maven {
                credentials {
                    username "$token_usr"
                    password "$token_pwd"
                }
                url "https://s01.oss.sonatype.org/content/repositories/snapshots"
            }
        }
        signing {
            sign publishing.publications.ardwloop
        }
    }
}